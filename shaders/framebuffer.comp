#version 460 core
layout(local_size_x = 8, local_size_y = 4, local_size_z = 1) in;
layout(rgba32f, binding = 0) uniform image2D screen;

const float EPSILON = 0.000001;
const float TMIN = 0.001;
const float TMAX = 10000.0;

float intersect_sphere(vec3 ray_origin, vec3 ray_dir, vec3 sphere_origin, float r)
{
	vec3 oc = ray_origin - sphere_origin;
	float a = dot(ray_dir, ray_dir);
	float b = 2.0 * dot(oc, ray_dir);
	float c = dot(oc, oc) - r * r;
	float discriminant = b * b - 4.0 * a * c;
	if(discriminant >= 0)
	{
		float sqrt_discriminant = sqrt(discriminant);
		if(abs(discriminant) <= EPSILON)
		{
			float t = -b / (2.0 * a);
			if(t > TMIN && t < TMAX)
			{
				return t;
			}
		}
		else
		{
			float t1 = (-b + sqrt_discriminant) / (2.0 * a);
			float t2 = (-b - sqrt_discriminant) / (2.0 * a);
			if(t1 > t2)
			{
				float tmp = t1;
				t1 = t2;
				t2 = tmp;
			}

			if(t1 > TMIN && t1 < TMAX)
			{
				return t1;
			}
			else if (t2 > TMIN && t2 < TMAX)
			{
				return t2;
			}
		}
	}

	return -1.0;
}

vec4 render_function(uvec2 screen_size, ivec2 pixel_coords)
{
	uint width = screen_size.x;
	uint height = screen_size.y;
	float aspect_ratio = float(width) / height;

	float grid_height = 2.0;
	float grid_width = aspect_ratio * grid_height;

	vec3 grid_x = vec3(grid_width, 0.0, 0.0);
	vec3 grid_y = vec3(0.0, -grid_height, 0.0);

	vec3 eye = vec3(0.0, 0.0, 0.0);
	vec3 grid_origin = eye - (grid_x * 0.5) - (grid_y * 0.5);
	grid_origin.z = -2.0;

	int x = pixel_coords.x;
	int y = pixel_coords.y;

	vec2 offset_to_pixel_center = vec2(0.5, 0.5);
	float u = x / float(width);
	float v = y / float(height);

	vec3 point_on_grid = grid_origin + u * grid_x + v * grid_y;
	vec3 ray_direction = normalize(point_on_grid - eye);

	vec3 sphere_origin = vec3(0.0, 0.0, -3.0);
	float sphere_radius = 1.0;

	float t = intersect_sphere(eye, ray_direction, sphere_origin, sphere_radius);

	if(t >= TMIN)
	{
		return vec4((normalize(eye + ray_direction * t - sphere_origin) + 1.0) / 2.0, 1.0);
	}

	return vec4(0.2, 0.2, 0.2, 1.0);
}

void main() 
{
	ivec2 pixel_coords = ivec2(gl_GlobalInvocationID.xy);
	
	uvec2 screen_size = imageSize(screen);
	vec4 color = render_function(screen_size, pixel_coords);

	imageStore(screen, pixel_coords, color);
}